import os
from templates import Manuscript

env = Environment()
env.SetDefault(TEXMFHOME=os.path.join(os.environ['HOME'], 'texmf'))
# To include the class and bibtex files in the 'classes' dir
env.Replace(TEXINPUTS='classes//:')
env.Replace(BSTINPUTS='classes//:')

# The paper metadata
builder = Manuscript(
    title='Optimal forward modeling of gravitational fields using tesseroids with adaptive discretization',
    author=['Leonardo Uieda',
            'Vanderlei C. Oliveira Jr',
            r'Val\'eria C. F. Barbosa'],
    affil=['Universidade do Estado do Rio de Janeiro, Rio de Janeiro, Brazil.',
           r'Observat\'orio Nacional, Rio de Janeiro, Brazil.'],
    author_affil=[[0, 1], [1], [1]],
    journal='gji',
    heads=['Uieda et al.', 'Optimal forward modeling using tesseroids'],
    bibfile='references.bib')

# Build the manuscript for submission (Latex and PDF files)
ms = env.Command('manuscript.tex', 'body.tex', builder.build_ms)
mspdf = env.PDF(target='manuscript.pdf', source=ms)
# Declare that the PDf depends on the figures so it gets rebuild when they
# change.
Depends(mspdf, Glob('figs/*.eps'))

# Build a nicer "published" version of the ms
paper = env.Command('paper.tex', 'body.tex', builder.build_paper)
paperpdf = env.PDF(target='paper.pdf', source=paper)
Depends(paperpdf, Glob('figs/*.eps'))

# These files get generated by pdflatex
Clean(os.path.curdir, Glob('*-eps-converted-to.pdf'))
# Generated by Python when importing templates.py
Clean(os.path.curdir, Glob('*.pyc'))
